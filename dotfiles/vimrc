set nocompatible

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vim-plug

if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -k -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/bundle')
Plug 'junegunn/vim-plug'    " has vim-plug help
" No harm plugins from https://www.vi-improved.org/plugins/
Plug 'https://github.com/tpope/vim-abolish'
Plug 'https://github.com/tpope/vim-characterize'
Plug 'https://github.com/tpope/vim-commentary'
Plug 'https://github.com/justinmk/vim-dirvish'
Plug 'https://github.com/tpope/vim-eunuch'
Plug 'https://github.com/tpope/vim-fugitive'
Plug 'https://github.com/ludovicchabant/vim-gutentags'
Plug 'https://github.com/tommcdo/vim-lion'
Plug 'https://github.com/romainl/vim-qf'
Plug 'https://github.com/romainl/vim-qlist'
Plug 'https://github.com/tpope/vim-repeat'
Plug 'https://github.com/tpope/vim-rsi'
Plug 'https://github.com/tpope/vim-sensible'
Plug 'https://github.com/justinmk/vim-sneak'
Plug 'https://github.com/tpope/vim-speeddating'
Plug 'https://github.com/tpope/vim-surround'
Plug 'https://github.com/wellle/targets.vim'
Plug 'https://github.com/tomtom/ttags_vim'
Plug 'https://github.com/mbbill/undotree'
Plug 'https://github.com/ajh17/VimCompletesMe'
Plug 'https://github.com/tpope/vim-unimpaired'
" Build
Plug 'neomake/neomake'
" Editor
Plug 'https://github.com/editorconfig/editorconfig-vim'
Plug 'https://github.com/xtal8/traces.vim'
Plug 'https://github.com/chaoren/vim-wordmotion'
Plug 'https://github.com/fvictorio/vim-yank-queue'
" Formatting
Plug 'https://github.com/z0mbix/vim-shfmt', { 'for': 'sh' }
" Search
Plug 'https://github.com/mileszs/ack.vim'
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --no-update-rc' }
Plug 'junegunn/fzf.vim'
" Specific for Java
Plug 'https://github.com/akhaku/vim-java-unused-imports'
" Specific for Python
Plug 'https://github.com/nvie/vim-flake8'
Plug 'https://github.com/vim-scripts/indentpython.vim'
Plug 'https://github.com/tmhedberg/SimpylFold'
" Specific for Tmux
Plug 'https://github.com/tmux-plugins/vim-tmux'
Plug 'https://github.com/tmux-plugins/vim-tmux-focus-events'
" Version control
Plug 'https://github.com/airblade/vim-gitgutter'
" Visuals
Plug 'https://github.com/c9s/colorselector.vim'
Plug 'https://github.com/Konfekt/FastFold'
" Visuals - colorschemes
" Plug 'https://github.com/rafi/awesome-vim-colorschemes' " several schemes aggregated in one place
call plug#end()

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vim settings

syntax on
filetype indent plugin on
set autoread
set backspace=indent,eol,start
set encoding=utf-8
set gcr=a:blinkon0  " Disable cursor blink
set history=1000  " Store lots of :cmdline history
set nolist
set magic
set number
set ruler
set showcmd
set showmatch  " show matching bracket
set showmode
set timeoutlen=2000
set visualbell

" Backup, swap etc.
set nobackup  " don't make automatic backups
set noswapfile
set nowb

" Colors if you have vim >= 8 or Neovim >= 0.1.5
if has("gui_running")
  if (has("termguicolors"))
    set termguicolors
    set t_ut=
  endif
endif

" Hidden
" This makes vim act like all other editors, buffers can
" exist in the background without being in a window.
" http://items.sjbach.com/319/configuring-vim-right
set hidden

" Scrolling
set scrolloff=8  " keeps cursor away from top/bottom of screen
set sidescrolloff=4
set sidescroll=0

" Search
set hlsearch
set ignorecase
set incsearch
set smartcase  " ignore case unless we type a capital
set wildmenu
" Don't offer to open certain files/directories
set wildignore+=*.bak
set wildignore+=*~,.*~
set wildignore+=*.bmp,*.gif,*.ico,*.jpg,*.png,*.ico
set wildignore+=*.dat,*.out
set wildignore+=*.exe,*.dll
set wildignore+=*.pdf,*.psd
set wildignore+=*.pyc
set wildignore+=*.so
set wildignore+=*stackdump
set wildignore+=*.swo,*.swp,tags,tags.lock
set wildignore+=*.\(zip\|7z\|bz2\|gz\|tar\|xz\|[zZ]\)
set wildignore+=[Dd]esktop.ini
set wildignore+=node_modules/*,bower_components/*
set wildignore+=.\(git\|hg\|svn\)
set wildignore+=.idea,*.sublime-project,*.sublime-workspace,.vscode
set wildignore+=.kitchen

" Theme options for the seoul256 colors:
let g:seoul256_background = 234
set background=dark

" Theme options for the zenburn colors:
let g:zenburn_transparent = 1
let g:zenburn_alternate_Visual = 1

" Themes
set t_Co=256
colorscheme seoul256

" Undo
if has('persistent_undo') && !isdirectory(expand('~').'/.vim/undodir')
  silent !mkdir ~/.vim/undodir > /dev/null 2>&1
  set undodir=~/.vim/undodir
  set undofile
endif

" Whitespace
set autoindent    " align the new line indent with the previous line
set expandtab     " insert spaces when hitting TABs
set shiftround    " round indent to multiple of 'shiftwidth'
set shiftwidth=2  " operation >> indents this number of columns; << unindents
set smartindent
set smarttab
set softtabstop=2 " insert/delete this num of spaces when hitting a TAB/BACKSPACE
set tabstop=2     " a hard TAB displays as this num of columns
set textwidth=127 " lines with more columns than this will be broken
set nowrap
set wrapmargin=0
set wrapscan

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Shortcuts

" Leaders
let mapleader = ","
let maplocalleader = "\\"

" By https://www.vi-improved.org/recommendations/
nnoremap <leader>a :argadd <c-r>=fnameescape(expand('%:p:h'))<cr>/*<C-d>
nnoremap <leader>b :b <C-d>
nnoremap <leader>e :e **/
nnoremap <leader>g :grep<space>
nnoremap <leader>i :Ilist<space>
nnoremap <leader>j :tjump /
nnoremap <leader>m :make<cr>
nnoremap <leader>q :b#<cr>
nnoremap <leader>t :TTags<space>*<space>*<space>.<cr>

" Completion
inoremap <silent> ;f <C-x><C-f>
inoremap <silent> ;i <C-x><C-i>
inoremap <silent> ;l <C-x><C-l>
inoremap <silent> ;n <C-x><C-n>
inoremap <silent> ;o <C-x><C-o>
inoremap <silent> ;t <C-x><C-]>
inoremap <silent> ;u <C-x><C-u>

" Copy
set pastetoggle=<F3>

" File open - gf - opens file under cursor in a new vertical split
nnoremap gf :vertical wincmd f<CR>

" Folding
set foldmethod=indent
set foldlevel=99
nnoremap <space> za

" Nav cd to current file dir
nnoremap <silent> <F4> :lchdir %:p:h<CR>:pwd<CR>

" Nav split panels with vim char motions
nnoremap <C-J> <C-W><C-J>
nnoremap <C-K> <C-W><C-K>
nnoremap <C-L> <C-W><C-L>
nnoremap <C-H> <C-W><C-H>

" Repeat also on visually selected lines
vnoremap . :norm.<CR>

" Search with Ack.vim and Ag silversearcher
map <Leader>a :Ack!<Space>
nmap <Leader>b :Buffers<CR>
nmap <Leader>t :Files<CR>

" Upcase last typed word
inoremap ;wu <esc>viwUea

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Ag silversearcher

if executable("ag")
  " Ack.vim invoked by typing ag, case insensitive:
  cnoreabbrev ag Ack!
  cnoreabbrev aG Ack!
  cnoreabbrev Ag Ack!
  cnoreabbrev AG Ack!

  " Ack.vim program change to Ag silversearcher
  let g:ackprg = 'ag --vimgrep --smart-case'

  " Default grep override
  set grepprg=ag\ --nogroup\ --nocolor\ --ignore-case\ --column
  set grepformat=%f:%l:%c:%m,%f:%l:%m
endif

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Auto make dir

function! <SID>AutoMkdir() abort
  let l:dir = expand('<afile>:p:h')
  let l:file = expand('<afile>:t')
  if !isdirectory(l:dir)
    call mkdir(l:dir, 'p')
    silent execute 'bw ' . l:dir . '/' . l:file
    silent execute 'e ' . l:dir . '/' . l:file
  endif
endfunction

augroup AutoMkdir
  autocmd!
  autocmd BufWritePre,FileWritePre,BufNewFile *
    \ call <SID>AutoMkdir()
augroup END

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Auto open quickfix window
augroup autoquickfix
  autocmd!
  autocmd QuickFixCmdPost [^l]* cwindow
  autocmd QuickFixCmdPost    l* lwindow
augroup END

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" fzf - fuzzy finder

let g:fzf_tags_command = 'ctags --extra=+f -R'
let g:fzf_colors =
\ { 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" GUI (graphical Vim)

if has("gui_running")
  " GUI is running or is about to start.
  set background=dark
  set gfn=Fira\ Code\ 11
  set lines=999 columns=140
endif

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" :H topic will open an 80 columns vertical split

command! -complete=help -nargs=1 H call VerticalHelp(<f-args>)
function! VerticalHelp(topic)
  execute "vertical botright help " . a:topic
  execute "vertical resize 78"
endfunction

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Highlight only the line with focus

augroup highlight_follows_focus
  autocmd!
  autocmd WinEnter * set cursorline
  autocmd WinLeave * set nocursorline
augroup END

augroup highlight_follows_vim
  autocmd!
  autocmd FocusGained * set cursorline
  autocmd FocusLost * set nocursorline
augroup END

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugin EditorConfig

let g:EditorConfig_exclude_patterns = ['fugitive://.*', 'scp://.*']

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugin Python folding SimpylFold

let g:SimpylFold_docstring_preview = 1

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugin Shfmt - Shell formatting, ex command is 'Shfmt'

" Use two spaces indentation by default:
let g:shfmt_extra_args = '-i 2'

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Whitespace

" BadWhitespace definition:
highlight BadWhitespace ctermbg=red guibg=darkred

" BadWhitespace opening file:
au BufRead,BufNewFile *.py,*.pyw,*.rb,*.c,*.h match BadWhitespace /\s\+$/

" Strip trailing whitespace function
nnoremap <leader>s :call StripTrailingWhitespace()<cr>
function! StripTrailingWhitespace()
  if !&binary && &filetype != 'diff'
    normal mz
    normal Hmy
    %s/\s\+$//e
    normal 'yz<CR>
    normal `z
  endif
endfunction

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Specific conf for Python files

au BufNewFile,BufRead *.py
  \ set tabstop=4 |
  \ set softtabstop=4 |
  \ set shiftwidth=4 |
  \ set textwidth=79 |
  \ set fileformat=unix

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Specific conf for Ruby files

au BufNewFile,BufRead *.erb,*.rb
  \ set fileformat=unix

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Specific conf for web files

au BufNewFile,BufRead *.js,*.html,*.css
  \ set fileformat=unix

" """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
